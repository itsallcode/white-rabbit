plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.4.0'
}

rootProject.name = 'white-rabbit'

include 'logic'
include 'jfxui'

include 'api'
include 'plugins'
include 'plugins:demo'
include 'plugins:holiday-calculator'
include 'plugins:csv'

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
    versionCatalogs {
        libs {
            version('junitJupiter', '5.10.2')
            version('mockito', '5.11.0')
            version('log4j', '2.23.1')
            version('javafx', '23-ea+3')

            library('junitParams', 'org.junit.jupiter', 'junit-jupiter-params').versionRef('junitJupiter')
            library('assertj', 'org.assertj:assertj-core:3.25.3')
            library('junitPioneer', 'org.junit-pioneer:junit-pioneer:2.2.0')
            library('equalsverifier', 'nl.jqno.equalsverifier:equalsverifier:3.16.1')
            library('tostringverifier', 'com.jparams:to-string-verifier:1.4.8')
            library('hamcrest', 'org.hamcrest:hamcrest-all:1.3')
            library('mockito', 'org.mockito', 'mockito-core').versionRef('mockito')
            library('mocktioJunit', 'org.mockito', 'mockito-junit-jupiter').versionRef('mockito')
            bundle('testUtils', ['assertj', 'junitPioneer', 'junitParams', 'equalsverifier', 'tostringverifier', 'hamcrest', 'mockito', 'mocktioJunit'])

            library('testfx', 'org.testfx:testfx-junit5:4.0.18')
            library('monocle', 'org.pdfsam:javafx-monocle:21')

            library('jsonBindApi', 'jakarta.json.bind:jakarta.json.bind-api:3.0.1')
            library('yasson', 'org.eclipse:yasson:3.0.3')

            library('jdtAnnotations', 'org.eclipse.jdt:org.eclipse.jdt.annotation:2.3.0')
            library('jansi', 'org.fusesource.jansi:jansi:2.4.1')
            library('log4j.api', 'org.apache.logging.log4j', 'log4j-api').versionRef('log4j')
            library('log4j.core', 'org.apache.logging.log4j', 'log4j-core').versionRef('log4j')
            library('log4j.jul', 'org.apache.logging.log4j', 'log4j-jul').versionRef('log4j')
            library('log4j.slf4j', 'org.apache.logging.log4j', 'log4j-slf4j-impl').versionRef('log4j')

            library('holidaycalculator', 'org.itsallcode:holiday-calculator:0.2.0')
        }
    }
}
