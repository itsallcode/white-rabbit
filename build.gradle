plugins {
    id 'eclipse'
    id "org.sonarqube" version "3.4.0.2513"
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.6"
    id "io.codearte.nexus-staging" version "0.30.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
    id "com.moowork.node" version "1.3.1" apply false
    id "com.github.ben-manes.versions" version "0.42.0"
    id "org.panteleyev.jpackageplugin" version "1.4.1" apply false
    id "org.gradle.crypto.checksum" version "1.4.0" apply false
}

def getProjectVersion() {
    if(project.hasProperty("releaseVersion")) {
        return project.property("releaseVersion")
    }
    return "1.8.0"
}

version = getProjectVersion()
group = "org.itsallcode.whiterabbit"

def javaVersion = project.hasProperty('javaVersion') ? project.getProperty('javaVersion') : 17

subprojects {
    group = rootProject.group
    version = rootProject.version

    apply plugin: "java"
    apply plugin: "jacoco"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion)
        }
        modularity.inferModulePath = false
    }

    tasks.withType(JavaExec) {
        javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    testing {
        suites {
            test {
                useJUnitJupiter(libs.versions.junitJupiter.get())
            }
        }
    }

    javadoc {
        enabled = false
        failOnError = true
        options.addBooleanOption('html5', true)
        options.addStringOption('Xwerror', '-quiet')
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
    }

    rootProject.tasks["sonarqube"].dependsOn(tasks["jacocoTestReport"])
}

def getOptionalProperty(String name) {
    if(project.hasProperty(name)) {
        return project.property(name)
    }
    logger.info("Project property '${name}' not available. Please it to ~/.gradle/gradle.properties")
    return null
}

nexusStaging {
    packageGroup = "org.itsallcode"
    username = getOptionalProperty("ossrhUsername")
    password = getOptionalProperty("ossrhPassword")
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "itsallcode"
    }
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  gradleReleaseChannel = "current"
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}
