
plugins {
  id "org.sonarqube" version "2.7"
}

version = '0.0.1'

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    
    ext {
        junitVersion = '5.4.2'
        log4jVersion = '2.11.2'
        mockitoVersion = '2.27.0'
        moduleName = project.name
    }

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencies {
        compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
        runtime "org.apache.logging.log4j:log4j-core:$log4jVersion"
    
        testCompile "org.assertj:assertj-core:3.12.2"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
        testCompile "org.mockito:mockito-junit-jupiter:$mockitoVersion"
        testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    }
    
    compileJava {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                '--module-path', classpath.asPath,
            ]
            classpath = files()  
        }
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        jcenter()
    }
    
    eclipse.classpath.file {
        whenMerged {
            entries.findAll { ['lib', 'src'].contains(it.kind) }.each { it.entryAttributes['module'] = 'true' }
        }
    }
}

