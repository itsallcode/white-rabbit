plugins {
    id 'eclipse'
    id "org.sonarqube" version "3.1.1"
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0"
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
    id "com.moowork.node" version "1.3.1" apply false
}

ext {
    junitVersion = '5.7.1'
    log4jVersion = '2.14.1'
    mockitoVersion = '3.8.0'
    yassonVersion = '2.0.2'
    jsonBindApiVersion = '1.0'
}

def getProjectVersion() {
    if(project.hasProperty("releaseVersion")) {
        return project.property("releaseVersion")
    }
    return "1.6.0-SNAPSHOT"
}

version = getProjectVersion()
group = "org.itsallcode.whiterabbit"

def nonJavaProjects = ['webstart-infrastructure', 'webstart']

subprojects {
    group = rootProject.group
    version = rootProject.version

    if(nonJavaProjects.contains(project.name)) {
        return
    }
    apply plugin: "java"
    apply plugin: "jacoco"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    dependencies {
        testImplementation "org.assertj:assertj-core:3.19.0"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
        testImplementation "org.junit-pioneer:junit-pioneer:1.3.8"
        testImplementation "nl.jqno.equalsverifier:equalsverifier:3.5.5"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testRuntimeOnly "org.apache.logging.log4j:log4j-core:$log4jVersion"
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    rootProject.tasks["sonarqube"].dependsOn(tasks["jacocoTestReport"])
}
