plugins {
    id "org.sonarqube" version "3.0"
}

ext {
    junitVersion = '5.6.2'
    log4jVersion = '2.13.3'
    mockitoVersion = '3.4.6'
}

def getProjectVersion() {
    if(project.hasProperty("releaseVersion")) {
        return project.property("releaseVersion")
    }
    return "dev-SNAPSHOT"
}

allprojects {
    apply plugin: "java"
    apply plugin: "jacoco"

    version = getProjectVersion()
    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        jcenter()
    }

    dependencies {
        implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-core:$log4jVersion"

        testImplementation "org.assertj:assertj-core:3.16.1"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    rootProject.tasks["sonarqube"].dependsOn(tasks["jacocoTestReport"])
}

