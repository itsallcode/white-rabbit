plugins {
    id 'java-library'
}

dependencies {
    api project(':api')
    implementation libs.log4j.api
    implementation libs.jsonBindApi
    implementation libs.yasson

    testImplementation libs.bundles.testUtils
}

ext.generatedResourcesFolder = file("$buildDir/generated-resources")

sourceSets {
    main {
        resources.srcDirs generatedResourcesFolder
    }
}

task createAppProperties(type: WriteProperties) {
    def buildDate = Instant.now().toString()
    comment = "White Rabbit app properties"
    destinationFile = file("$generatedResourcesFolder/white-rabbit.properties")
    property 'version', project.version
    property 'buildDate', buildDate
}

tasks['processResources'].dependsOn createAppProperties

eclipse {
    synchronizationTasks createAppProperties
}

idea.project.settings {
    taskTriggers {
        afterSync createAppProperties
    }
}
